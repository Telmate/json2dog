#!/usr/bin/perl

use strict;
use warnings;

use sigtrap qw/handler  handle_death  normal-signals/;

use Data::Dumper;
use LWP::Simple qw(get);
use JSON;
use IO::Handle;

use DataDog::DogStatsd;

# options
my $debug = $ENV{JSON2DOG_DEBUG} || 0;
my $log_file = $ENV{JSON2DOG_LOG} || undef;
my $pid_file = $ENV{PIDFILE} || undef;
my $delay = 60; # seconds

my $use_curl = 0; # true if curl is better than LWP::Simple
#$use_curl = 1 if -e '/etc/redhat-release';
#print "use_curl = $use_curl\n";

# read command line arguments
my ($health_url,$statsd_base) = @ARGV;

daemonize();

# open log file
our $log_fh;
if (defined $log_file) {
	open $log_fh, '>>', $log_file or die "couldn't open $log_file: $!";
} else {
	# log to STDOUT
	open $log_fh, '>&STDOUT' or die "couldn't copy STDOUT: $!";
}

# defaults
unless (defined $health_url) {
	$health_url = 'https://jsonplaceholder.typicode.com/posts/1';
	error("checking $health_url");
}

unless (defined $statsd_base) {
	$statsd_base = 'test.json';
	error("defaulting stats_base to '$statsd_base'.");
}
my @log_minutes = qw( 1 10 60 ); # and every 60x minutes
my $stats_sent = 0; # initialize counter

# instances of handy objects
my $dogstatsd = DataDog::DogStatsd->new;
$dogstatsd->{port} = find_port();
my $json = JSON->new->allow_nonref;

# keep pulling stats until we get killed
while (1) {
	my $raw_html = http_get($health_url);

	unless (defined $raw_html and length $raw_html) {
		my $sleep_time = int(rand(30));
		error("no data back from $health_url, sleeping for $sleep_time seconds");
		sleep($sleep_time);
		next; # try again
	}

	my $health_resp = $json->decode($raw_html);
	error(Dumper($health_resp)) if $debug > 1;

	foreach my $key (keys %$health_resp) {
		my $value = $health_resp->{$key};
		if ($value =~ /^[0-9.]+$/) { # only numbers
			my $metric_label = $statsd_base . '.' . $key;
			error("$metric_label -> $value") if $debug;
			$dogstatsd->gauge( $metric_label, $value);
			$stats_sent++;
		}
	}

	timed_log($stats_sent);

	error("") if $debug;
	sleep($delay);
}

sub http_get {
	my ($url) = @_;

	my $raw;
	if ($use_curl) {
		$raw = get($url); # LWP::Simple
	} else {
		$raw = curl($url);
	}

	#print "$raw\n";
	return $raw;
}

sub curl {
	my ($url) = @_;
	my $output = `curl -s $url`;
	return $output;
}

sub find_port {
	my @possible_ports = qw(8125 1025);

	if ( defined $ENV{DOGSTATSD_PORT} ) {
		unshift(@possible_ports, $ENV{DOGSTATSD_PORT});
	}

	foreach my $port (@possible_ports) {
		my $output = `netstat -na | grep :$port`;
		if (length($output)) {
			error("talking to port $port");
			return $port;
		}
	}

	error("ERROR: no dogstatsd port to talk to");
	die;
}

sub error {
	my ($log_message) = @_;

	my $now = gmtime(time);
	my $hostname = 'localhost:';
	if (defined $ENV{HOSTNAME}) {
		$hostname = $ENV{HOSTNAME} . ':';
	}

	print $log_fh "$now ${hostname}[$$] $log_message\n";
	$log_fh->flush();
}

sub handle_death {
	my $sig_name = shift;
	error("exiting on signal $sig_name");
	exit(1);
}

sub timed_log {
	my ($log_message) = @_;

	my $running = (time - $^T) / 60;
	$running = sprintf("%.1f",$running);

	my $show_minutes = $log_minutes[0];

	return unless $running > $show_minutes;

	error("$log_message metrics sent over $running minutes");

	shift @log_minutes;
	unless (scalar @log_minutes) {
		# empty @log_minutes
		push(@log_minutes, $show_minutes + 60);
		error("adding to log_minutes $show_minutes+60") if $debug;
	}
	#die;
}

# mostly from https://stackoverflow.com/a/768448/2002471
sub daemonize {
	use POSIX;
	POSIX::setsid or die "setsid: $!";

	my $pid = fork ();
	if ($pid < 0) {
		die "fork: $!";
	} elsif ($pid) {
		exit 0;
	}

	chdir "/";
	umask 0;
	foreach (0 .. (POSIX::sysconf (&POSIX::_SC_OPEN_MAX) || 1024)) {
		POSIX::close $_;
	}
	open (STDIN, "</dev/null");
	open (STDOUT, ">/dev/null");
	open (STDERR, ">&STDOUT");

	if (defined $pid_file) {
		open(my $pid_fh,'>',$pid_file) or die "couldn't open(>$pid_file): $!";
		print $pid_fh "$$\n";
		close($pid_fh) or die "couldn't close(>$pid_file): $!";
	}
}
